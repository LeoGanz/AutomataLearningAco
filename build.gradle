import ru.vyarus.gradle.plugin.python.PythonExtension

plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'ru.vyarus.use-python' version '2.3.0'
}

repositories {
    mavenCentral()
}

application {
    mainClass = 'ganz.leonard.automatalearning.Main'
}

tasks.withType(Test) {
    jvmArgs += ['--add-opens', 'javafx.base/javafx.util=ALL-UNNAMED']
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'guru.nidi:graphviz-java:0.18.1'
    implementation 'com.eclipsesource.j2v8:j2v8_win32_x86_64:4.6.0'
    implementation 'com.eclipsesource.j2v8:j2v8_linux_x86_64:4.8.0'
    implementation 'ch.qos.logback:logback-classic:1.2.10'
    implementation 'org.imgscalr:imgscalr-lib:4.2'
    implementation 'com.miglayout:miglayout-swing:5.3'
    implementation 'io.soabase.record-builder:record-builder-core:29'
    implementation 'org.csveed:csveed:0.7.2'
    implementation 'org.slf4j:slf4j-simple:2.0.0-alpha5'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation("com.google.guava:guava:31.0.1-jre")
    annotationProcessor 'io.soabase.record-builder:record-builder-processor:29'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.8.2'
}

System.setProperty("org.checkstyle.google.suppressionfilter.config", "$rootProject.projectDir/config/checkstyle/checkstyle-suppressions.xml")
checkstyle {
    toolVersion '8.43'
    maxWarnings = 0
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    filter {
//        excludeTestsMatching("ganz.leonard.automatalearning.altests.*") // used for parameter optimization; no unit tests
    }
}

task copyPythonCode(type: Copy) {
    from 'src/main/python'
    into sourceSets.getByName("main").output.resourcesDir
}
classes.dependsOn copyPythonCode

pipUpdates.all = true
python {
    pip 'greenery:3.3.2'
    scope = PythonExtension.Scope.VIRTUALENV
}
