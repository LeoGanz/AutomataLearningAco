import ru.vyarus.gradle.plugin.python.PythonExtension

plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'ru.vyarus.use-python' version '2.3.0'
}

repositories {
    mavenCentral()
}

application {
    mainClass = 'ganz.leonard.automatalearning.Main'
}

tasks.withType(Test) {
    jvmArgs += ['--add-opens', 'javafx.base/javafx.util=ALL-UNNAMED']
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'guru.nidi:graphviz-java:0.18.1'
    implementation 'com.eclipsesource.j2v8:j2v8_win32_x86_64:4.6.0'
    implementation 'com.eclipsesource.j2v8:j2v8_linux_x86_64:4.8.0'
    implementation 'ch.qos.logback:logback-classic:1.2.5'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.7'
}

System.setProperty("org.checkstyle.google.suppressionfilter.config", "$rootProject.projectDir/config/checkstyle/checkstyle-suppressions.xml")
checkstyle {
    toolVersion '8.43'
    maxWarnings = 0
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task copyPythonCode(type: Copy) {
    from 'src/main/python'
    into sourceSets.getByName("main").output.resourcesDir
}
classes.dependsOn copyPythonCode

pipUpdates.all = true
python {
    pip 'greenery:3.3.2'
    scope = PythonExtension.Scope.VIRTUALENV
}
